# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("./../../../multimedia_player_framework_aafwk.gni")

subsystem_name = "multimedia"
part_name = "player_framework"
taihe_generated_file_path_audioHaptic = "$taihe_file_path/out/$subsystem_name/$part_name/audioHaptic"

copy_taihe_idl("copy_audioHaptic_taihe") {
  sources = [
    "${multimedia_player_framework_path}/frameworks/taihe/idl/ohos.multimedia.audioHaptic.audioHaptic.taihe",
    "${multimedia_player_framework_path}/frameworks/taihe/idl/ohos.multimedia.audioHaptic.taihe",
  ]
}

ohos_taihe("run_audioHaptic_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_audioHaptic}"
  deps = [ ":copy_audioHaptic_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.multimedia.audioHaptic.audioHaptic.ani.cpp",
    "$taihe_generated_file_path/src/ohos.multimedia.audioHaptic.audioHaptic.abi.c",
  ]
}

taihe_shared_library("audioHaptic_taihe") {
  stack_protector_ret = true
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "${taihe_generated_file_path_audioHaptic}"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  include_dirs = [
    "include/",
    "${multimedia_player_framework_path}/frameworks/taihe/common",
    "${multimedia_player_framework_path}/frameworks/taihe/media/include",
    "${multimedia_player_framework_path}/interfaces/inner_api/native",
    "${multimedia_player_framework_path}/interfaces/inner_api/native/audio_haptic/include",
    "${multimedia_player_framework_path}/services/utils/include",
  ]

  sources = get_target_outputs(":run_audioHaptic_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/audio_haptic_common_taihe.cpp",
    "src/audio_haptic_manager_taihe.cpp",
    "src/audio_haptic_player_callback_taihe.cpp",
    "src/audio_haptic_player_taihe.cpp",
    "${multimedia_player_framework_path}/frameworks/taihe/common/common_taihe.cpp",
  ]

  deps = [
    ":run_audioHaptic_taihe",
    "${multimedia_player_framework_path}/frameworks/native/audio_haptic:audio_haptic",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "audio_framework:audio_client",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_single",
    "media_foundation:media_foundation",
  ]
}

generate_static_abc("audioHaptic_taihe_abc") {
  base_url = "${taihe_generated_file_path_audioHaptic}"
  files = [ "${taihe_generated_file_path_audioHaptic}/@ohos.multimedia.audioHaptic.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/audioHaptic_taihe_abc.abc"
  dependencies = [ ":run_audioHaptic_taihe" ]
}

ohos_prebuilt_etc("audioHaptic_etc") {
  source = "$target_out_dir/audioHaptic_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":audioHaptic_taihe_abc" ]
}