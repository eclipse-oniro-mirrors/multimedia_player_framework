# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/player_framework/config.gni")

MEDIA_ROOT_DIR = "//foundation/multimedia/player_framework"

config("media_public_config") {
  include_dirs = [
    "$MEDIA_ROOT_DIR/interfaces/inner_api/native",
    "$MEDIA_ROOT_DIR/services/utils/include",
  ]

  defines = [ "IMAGE_COLORSPACE_FLAG" ]
  defines += player_framework_defines
}

config("media_client_ipc_config") {
  include_dirs = [
    "$MEDIA_ROOT_DIR/services/services/sa_media/client",
    "$MEDIA_ROOT_DIR/services/services/sa_media/ipc",
    "$MEDIA_ROOT_DIR/services/services/player/client",
    "$MEDIA_ROOT_DIR/services/services/player/ipc",
    "$MEDIA_ROOT_DIR/services/services/media_data_source/ipc",
    "$MEDIA_ROOT_DIR/services/services/dolby_passthrough/ipc",
    "$MEDIA_ROOT_DIR/services/services/media_source/ipc",
    "$MEDIA_ROOT_DIR/services/services/monitor/client",
    "$MEDIA_ROOT_DIR/services/services/monitor/ipc",
    "$MEDIA_ROOT_DIR/services/services/recorder/client",
    "$MEDIA_ROOT_DIR/services/services/recorder/ipc",
    "$MEDIA_ROOT_DIR/services/services/avmetadatahelper/client",
    "$MEDIA_ROOT_DIR/services/services/avmetadatahelper/ipc",
    "$MEDIA_ROOT_DIR/services/services/common",
    "$MEDIA_ROOT_DIR/services/services/recorder_profiles/client",
    "$MEDIA_ROOT_DIR/services/services/recorder_profiles/ipc",
    "$MEDIA_ROOT_DIR/services/services/screen_capture/ipc",
    "$MEDIA_ROOT_DIR/services/services/screen_capture/client",
    "$MEDIA_ROOT_DIR/services/services/screen_capture_monitor/ipc",
    "$MEDIA_ROOT_DIR/services/services/screen_capture_monitor/client",
    "$MEDIA_ROOT_DIR/services/services/transcoder/ipc",
    "$MEDIA_ROOT_DIR/services/services/transcoder/client",
    "$MEDIA_ROOT_DIR/services/services/lpp_audio_streamer/ipc",
    "$MEDIA_ROOT_DIR/services/services/lpp_audio_streamer/client",
    "$MEDIA_ROOT_DIR/services/services/lpp_video_streamer/ipc",
    "$MEDIA_ROOT_DIR/services/services/lpp_video_streamer/client",
    "$MEDIA_ROOT_DIR/frameworks/native/media_source_loading_request",
  ]
}

config("media_client_base_config") {
  include_dirs = [
    "$MEDIA_ROOT_DIR/interfaces/inner_api/native",
    "$MEDIA_ROOT_DIR/services/include",
    "$MEDIA_ROOT_DIR/services/utils/include",
    "$MEDIA_ROOT_DIR/services/services/engine_intf",
  ]

  cflags = [
    "-std=c++17",
    "-fno-rtti",
    "-fno-exceptions",
    "-Wall",
    "-fno-common",
    "-fstack-protector-strong",
    "-Wshadow",
    "-FPIC",
    "-FS",
    "-O2",
    "-D_FORTIFY_SOURCE=2",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wdate-time",
    "-Werror",
    "-Wextra",
    "-Wimplicit-fallthrough",
    "-Wsign-compare",
    "-Wunused-parameter",
  ]

  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  defines = []
  defines += player_framework_defines

  if (player_framework_feature_pc_select_window) {
    defines += [ "PC_STANDARD" ]
  }

}

ohos_shared_library("media_client") {
  branch_protector_ret = "pac_ret"
  install_enable = true

  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [
    "$MEDIA_ROOT_DIR/frameworks/native/common/media_errors.cpp",
    "$MEDIA_ROOT_DIR/frameworks/native/common/lpp_common.cpp",
    "$MEDIA_ROOT_DIR/services/services/common/avsharedmemory_ipc.cpp",
    "$MEDIA_ROOT_DIR/services/services/monitor/client/monitor_client.cpp",
    "$MEDIA_ROOT_DIR/services/services/monitor/client/monitor_client_object.cpp",
    "$MEDIA_ROOT_DIR/services/services/monitor/ipc/monitor_service_proxy.cpp",
    "$MEDIA_ROOT_DIR/services/services/sa_media/client/media_client.cpp",
    "$MEDIA_ROOT_DIR/services/services/sa_media/ipc/media_listener_stub.cpp",
    "$MEDIA_ROOT_DIR/services/services/sa_media/ipc/media_parcel.cpp",
    "$MEDIA_ROOT_DIR/services/services/sa_media/ipc/media_reply_stub.cpp",
    "$MEDIA_ROOT_DIR/services/services/sa_media/ipc/media_service_proxy.cpp",
  ]
  if (player_framework_support_player) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/media_source_loading_request/loading_request_impl.cpp",
      "$MEDIA_ROOT_DIR/frameworks/native/player/player_impl.cpp",
      "$MEDIA_ROOT_DIR/frameworks/native/player/hiappevent_agent.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_data_source/ipc/media_data_source_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/dolby_passthrough/ipc/dolby_passthrough_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/ipc/media_source_loader_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/ipc/media_source_loading_request_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/player/client/player_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/player/ipc/player_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/player/ipc/player_service_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/loader/cache_manager.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/loader/file_cache_manager.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/loader/media_loader.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/loader/http_header_parser.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/loader/request_handler.cpp",
      "$MEDIA_ROOT_DIR/services/services/media_source/loader/custom_loader_callback.cpp",
      "$MEDIA_PLAYER_ROOT_DIR/services/services/media_source/loader/network_client_agent.cpp",
    ]
  }
  if (player_framework_support_recorder) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/recorder/recorder_impl.cpp",
      "$MEDIA_ROOT_DIR/frameworks/native/recorder_profiles/recorder_profiles_impl.cpp",
      "$MEDIA_ROOT_DIR/services/services/recorder/client/recorder_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/recorder/ipc/recorder_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/recorder/ipc/recorder_service_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/recorder_profiles/client/recorder_profiles_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/recorder_profiles/ipc/recorder_profiles_service_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/sa_media/ipc/recorder_profiles_parcel.cpp",
    ]
  }
  if (player_framework_support_metadata) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/avmetadatahelper/avmetadatahelper_impl.cpp",
      "$MEDIA_ROOT_DIR/services/services/avmetadatahelper/client/avmetadatahelper_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/avmetadatahelper/ipc/avmetadatahelper_service_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/avmetadatahelper/ipc/helper_listener_stub.cpp",
    ]
  }
  if (player_framework_support_screen_capture) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/screen_capture/screen_capture_controller_impl.cpp",
      "$MEDIA_ROOT_DIR/frameworks/native/screen_capture/screen_capture_impl.cpp",
      "$MEDIA_ROOT_DIR/frameworks/native/screen_capture/screen_capture_monitor_impl.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture/client/screen_capture_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture/client/screen_capture_controller_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture/ipc/screen_capture_controller_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture/ipc/screen_capture_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture/ipc/screen_capture_service_proxy.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture_monitor/client/screen_capture_monitor_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture_monitor/ipc/screen_capture_monitor_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/screen_capture_monitor/ipc/screen_capture_monitor_service_proxy.cpp",
    ]
  }
  if (player_framework_support_transcoder) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/transcoder/transcoder_impl.cpp",
      "$MEDIA_ROOT_DIR/services/services/transcoder/client/transcoder_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/transcoder/ipc/transcoder_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/transcoder/ipc/transcoder_service_proxy.cpp",
    ]
  }

  if (player_framework_support_lowpower_av_sink) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/lpp_audio_streamer/audio_streamer_impl.cpp",
      "$MEDIA_ROOT_DIR/services/services/lpp_audio_streamer/client/lpp_audio_streamer_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/lpp_audio_streamer/ipc/lpp_audio_streamer_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/lpp_audio_streamer/ipc/lpp_audio_streamer_service_proxy.cpp",
    ]
  }

  if (player_framework_support_lowpower_av_sink) {
    sources += [
      "$MEDIA_ROOT_DIR/frameworks/native/lpp_video_streamer/video_streamer_impl.cpp",
      "$MEDIA_ROOT_DIR/services/services/lpp_video_streamer/client/lpp_video_streamer_client.cpp",
      "$MEDIA_ROOT_DIR/services/services/lpp_video_streamer/ipc/lpp_video_streamer_listener_stub.cpp",
      "$MEDIA_ROOT_DIR/services/services/lpp_video_streamer/ipc/lpp_video_streamer_service_proxy.cpp",
    ]
  }

  configs = [
    ":media_client_base_config",
    ":media_client_ipc_config",
  ]

  public_configs = [ ":media_public_config" ]
  deps = [ "$MEDIA_ROOT_DIR/services/utils:media_service_utils" ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "audio_framework:audio_client",
    "av_codec:av_codec_client",
    "c_utils:utils",
    "drivers_interface_display:display_commontype_idl_headers",
    "graphic_surface:surface",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "hitrace:libhitracechain",
    "image_framework:image_native",
    "init:libbegetutil",
    "ipc:ipc_single",
    "netmanager_base:net_security_config_if",
    "qos_manager:qos",
    "samgr:samgr_proxy",
    "hdf_core:libhdi",
  ]

  if (player_framework_support_libcurl) {
    external_deps += [
      "av_codec:http_curl_client",
    ]
  } else {
    external_deps += [
      "histreamer_ext:http_curl_client",
    ]
  }

  if (player_framework_support_lowpower_av_sink) {
    external_deps += [
      "drivers_interface_lpplayer:liblow_power_player_proxy_1.0",
    ]
  }

  public_external_deps = [
    "graphic_2d:color_manager",
    "media_foundation:media_foundation",
  ]

  if (player_framework_support_video) {
    external_deps += [ "graphic_surface:surface" ]
  }

  if (player_framework_support_drm) {
    external_deps += [ "drm_framework:drm_framework" ]
  }

  if (player_framework_support_hiappevent) {
    external_deps += [ "hiappevent:hiappevent_innerapi" ]
  }

  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "multimedia"
  part_name = "player_framework"
  version_script = "player_framework.versionscript"
}
