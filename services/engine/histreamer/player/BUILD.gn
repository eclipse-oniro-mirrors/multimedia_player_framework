# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/player_framework/config.gni")
import("./../../../../multimedia_player_framework_aafwk.gni")

config("media_engine_histreamer_player_config") {
  visibility = [ ":*" ]

  cflags = [
    "-O2",
    "-fPIC",
    "-Wall",
    "-fexceptions",
    "-fno-rtti",
    "-Wno-unused-but-set-variable",
    "-Wno-format",
  ]
  cflags_cc = cflags

  include_dirs = [
    "$MEDIA_PLAYER_ROOT_DIR/services/engine/histreamer/player",
    "$MEDIA_PLAYER_ROOT_DIR/services/engine/histreamer/utils",
    "$MEDIA_PLAYER_ROOT_DIR/services/utils/include",
    "$MEDIA_PLAYER_ROOT_DIR/interfaces/inner_api/native",
    "$MEDIA_PLAYER_ROOT_DIR/services/services/engine_intf",
    "$MEDIA_PLAYER_ROOT_DIR/services/services/dolby_passthrough/ipc",
  ]
  defines = [ "MEDIA_OHOS" ]

  if (target_cpu == "arm64" || is_emulator) {
    dragging_player_path = "\"/system/lib64\""
  } else {
    dragging_player_path = "\"/system/lib\""
  }

  defines += [ "DRAGGING_PLAYER_PATH=${dragging_player_path}" ]

  defines += player_framework_defines
}

ohos_static_library("media_engine_histreamer_player") {
  stack_protector_ret = true
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = [
    "dfx_agent.cpp",
    "dragging_player_agent.cpp",
    "hiplayer_callback_looper.cpp",
    "hiplayer_impl.cpp",
    "live_controller.cpp",
    "seek_agent.cpp",
  ]

  configs = [
    ":media_engine_histreamer_player_config",
    "$MEDIA_PLAYER_ROOT_DIR/services/dfx:media_service_log_dfx_public_config",
    "$MEDIA_PLAYER_ROOT_DIR/services/utils:media_service_utils_public_config",
  ]

  deps = [
    "$MEDIA_PLAYER_ROOT_DIR/services/dfx:media_service_log_dfx",
    "$MEDIA_PLAYER_ROOT_DIR/services/utils:media_service_utils",
  ]

  external_deps = [
    "audio_framework:audio_client",
    "audio_framework:audio_renderer",
    "av_codec:av_codec_client",
    "av_codec:av_codec_media_engine_filters",
    "av_codec:av_codec_media_engine_modules",
    "c_utils:utils",
    "graphic_surface:surface",
    "graphic_surface:sync_fence",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "hitrace:libhitracechain",
    "init:libbegetutil",
    "ipc:ipc_single",
    "media_foundation:media_foundation",
    "safwk:system_ability_fwk",
  ]

  if (player_framework_support_drm) {
    external_deps += [ "drm_framework:drm_framework" ]
  }

  subsystem_name = "multimedia"
  part_name = "player_framework"
}
