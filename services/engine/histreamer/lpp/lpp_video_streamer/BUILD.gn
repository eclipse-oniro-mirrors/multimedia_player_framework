# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/player_framework/config.gni")

config("media_engine_histreamer_lpp_video_streamer_config") {
  visibility = [ ":*" ]

  defines = [
    "HST_ANY_WITH_NO_RTTI",
    "MEDIA_OHOS",
  ]

  if (player_framework_support_lowpower_av_sink) {
    defines += [ "SUPPORT_LPP" ]
    defines += [ "SUPPORT_LPP_AUDIO_STRAMER" ]
    defines += [ "SUPPORT_LPP_VIDEO_STRAMER" ]
  } else {
    defines += [ "UNSUPPORT_LPP_AUDIO_STRAMER" ]
    defines += [ "UNSUPPORT_LPP_VIDEO_STRAMER" ]
    defines += [ "UNSUPPORT_LPP" ]
  }

  cflags = [
    "-O2",
    "-fPIC",
    "-Wall",
    "-fexceptions",
    "-fno-rtti",
    "-Wno-unused-but-set-variable",
    "-Wno-format",
  ]
  cflags_cc = cflags

  include_dirs = [
    "$MEDIA_PLAYER_ROOT_DIR/services/engine/histreamer/lpp/lpp_video_streamer",
    "$MEDIA_PLAYER_ROOT_DIR/services/engine/histreamer/lpp/lpp_sync_manager",
    "$MEDIA_PLAYER_ROOT_DIR/services/engine/histreamer/lpp/lpp_engine_manager",
    "$MEDIA_PLAYER_ROOT_DIR/services/utils/include",
    "$MEDIA_PLAYER_ROOT_DIR/interfaces/inner_api/native",
    "$MEDIA_PLAYER_ROOT_DIR/services/services/engine_intf",
  ]
}

ohos_static_library("media_engine_histreamer_lpp_video_streamer") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  sources = [
    "$MEDIA_PLAYER_ROOT_DIR/frameworks/native/common/lpp_common.cpp",
    "$MEDIA_PLAYER_ROOT_DIR/frameworks/native/common/media_lpp_errors.cpp",
    "$MEDIA_PLAYER_ROOT_DIR/frameworks/native/common/media_errors.cpp",
    "./src/hilpp_vstreamer_impl.cpp",
    "./src/lpp_video_data_manager.cpp",
    "./src/lpp_vdec_adapter.cpp",
    "./src/lpp_video_callback_looper.cpp",
  ]

  configs = [
    ":media_engine_histreamer_lpp_video_streamer_config",
    "$MEDIA_PLAYER_ROOT_DIR/services/dfx:media_service_log_dfx_public_config",
    "$MEDIA_PLAYER_ROOT_DIR/services/utils:media_service_utils_public_config",
  ]

  deps = [
    "$MEDIA_PLAYER_ROOT_DIR/services/dfx:media_service_log_dfx",
    "$MEDIA_PLAYER_ROOT_DIR/services/utils:media_service_utils",
  ]

  external_deps = [
    "audio_framework:audio_client",
    "audio_framework:audio_renderer",
    "av_codec:av_codec_client",
    "av_codec:av_codec_media_engine_filters",
    "c_utils:utils",
    "graphic_surface:surface",
    "graphic_surface:sync_fence",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_single",
    "media_foundation:media_foundation",
    "safwk:system_ability_fwk",
  ]

  if (player_framework_support_lowpower_av_sink) {
    external_deps += [ 
      "drivers_interface_lpplayer:liblow_power_player_proxy_1.0",
      "hdf_core:libhdi",
    ]
  }

  subsystem_name = "multimedia"
  part_name = "player_framework"
}